const LOCALID_TWIN = 1
const LOCALID_MOM = 4
const LOCALID_RIVAL = 5
const LOCALID_BIRCH = 6
const VAR_DOOR_X = VAR_0x8009
const VAR_DOOR_Y = VAR_0x800A
const NeedPokemonTriggerLeft = 0
const NeedPokemonTriggerRight = 1
const VAR_GIVE_RUNNING_SHOES_TRIGGER = VAR_0x8008


mapscripts LittlerootTown_MapScripts  {
    MAP_SCRIPT_ON_TRANSITION {
		if (!flag(FLAG_VISITED_LITTLEROOT_TOWN)) {
            setflag(FLAG_VISITED_LITTLEROOT_TOWN)

            checkplayergender
            if (var(VAR_RESULT) == MALE) {
                setrespawn(HEAL_LOCATION_LITTLEROOT_TOWN_BRENDANS_HOUSE_2F)
            } else {
                setrespawn(HEAL_LOCATION_LITTLEROOT_TOWN_MAYS_HOUSE_2F)
            }
        }

    	clearflag (FLAG_HIDE_LITTLEROOT_TOWN_FAT_MAN)
    	call (Common_EventScript_SetupRivalGfxId)

   		if (var(VAR_LITTLEROOT_INTRO_STATE) == 2) {call (LittlerootTown_EventScript_MoveMomToMaysDoor)}

   		if (!flag(FLAG_RESCUED_BIRCH)) {call (LittlerootTown_EventScript_SetTwinPos)}

   		if (var(VAR_LITTLEROOT_TOWN_STATE) == 3) {call (LittlerootTown_EventScript_SetMomStandingInFrontOfDoorPos)}

    	if (var(VAR_LITTLEROOT_HOUSES_STATE_MAY) == 4) {call (LittlerootTown_EventScript_SetExitedHouseAfterLatiSSTicketEvent)}

    	if (var(VAR_LITTLEROOT_HOUSES_STATE_BRENDAN) == 4) {call (LittlerootTown_EventScript_SetExitedHouseAfterLatiSSTicketEvent)}

    	if (var(VAR_OLDALE_RIVAL_STATE) == 1) {call (LittlerootTown_EventScript_MoveRivalFromOldale)}

    	if (var(VAR_LITTLEROOT_RIVAL_STATE) == 3) {call (LittlerootTown_EventScript_SetRivalLeftForRoute103)}

    	if (var(VAR_DEX_UPGRADE_JOHTO_STARTER_STATE) == 1) { call (LittlerootTown_EventScript_HideMapNamePopup)}

    	if (var(VAR_DEX_UPGRADE_JOHTO_STARTER_STATE) == 2) {call (LittlerootTown_EventScript_LeftLabAfterDexUpgrade)}

    	end
	}
    
	MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_DEX_UPGRADE_JOHTO_STARTER_STATE, 1: LittlerootTown_EventScript_BeginDexUpgradeScene
    ]
    
	MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE[
		VAR_DEX_UPGRADE_JOHTO_STARTER_STATE, 1: LittlerootTown_EventScript_SetRivalBirchPosForDexUpgrade
	]
}

	// State descriptions for VAR_LITTLEROOT_TOWN_STATE
	// 1: Met Rival
	// 2: Saving Birch
	// 3: Received Pokedex
	// 4: Received Running Shoes

	// State descriptions for VAR_LITTLEROOT_INTRO_STATE
	// OLD# 					
	// 1: In truck male 				// wird übersprungen da der Spieler im Schlafzimmer spawned
	// 2: In truck female 			// wird übersprungen da der Spieler im Schlafzimmer spawned
	// 3: Entered house 				// wird übersprungen da der Spieler im Schlafzimmer spawned
	// 4: Told to go set clock 		// wird übersprungen da der Spieler im Schlafzimmer spawned
	// 5: Entered 2F to set clock 	// wird übersprungen da der Spieler im Schlafzimmer spawned
	// 6: Set clock 					// wird übersprungen da der Spieler im Schlafzimmer spawned
	// 7: Told to go meet rival

	// State descriptions for VAR_LITTLEROOT_HOUSES_STATE_MAY
	//                    and VAR_LITTLEROOT_HOUSES_STATE_BRENDAN
	// 1: On Truck (and is corresponding gender)
	// 2: Met Rival's Mom (and is corresponding gender)
	// 3: Entered Hall of Fame
	// 4: Completed Lati TV / SS Ticket event
	// 5: Exited house after above event

	// State descriptions for VAR_LITTLEROOT_RIVAL_STATE
	// 1: Never occurs
	// 2: Entered Rival's bedroom
	// 3: Met Rival
	// 4: Received Pokedex

script LittlerootTown_EventScript_BeginDexUpgradeScene{
	lockall
	playse (SE_PIN)
	applymovement (LOCALID_BIRCH, Common_Movement_ExclamationMark)
	waitmovement (0)
	delay (80)
	msgbox (LittlerootTown_Text_BirchSomethingToShowYouAtLab, MSGBOX_DEFAULT)
	closemessage
	clearflag (FLAG_HIDE_LITTLEROOT_TOWN_RIVAL)
	clearflag (FLAG_HIDE_LITTLEROOT_TOWN_BIRCH)
	delay (20)
	clearflag (FLAG_HIDE_MAP_NAME_POPUP)
	warp (MAP_LITTLEROOT_TOWN_PROFESSOR_BIRCHS_LAB, 6, 5)
	waitstate
	releaseall
	end
}

script LittlerootTown_EventScript_SetRivalBirchPosForDexUpgrade {
    addobject(LOCALID_BIRCH)
    addobject(LOCALID_RIVAL)
    checkplayergender
    if (var(VAR_RESULT) == MALE) {
        goto(LittlerootTown_EventScript_SetRivalBirchPosForDexUpgradeMale)
    } else {
        goto(LittlerootTown_EventScript_SetRivalBirchPosForDexUpgradeFemale)
    }
    end
}

script LittlerootTown_EventScript_SetRivalBirchPosForDexUpgradeMale {
    setobjectxy(LOCALID_RIVAL, 6, 10)
    setobjectxy(LOCALID_BIRCH, 5, 10)
    end
}

script LittlerootTown_EventScript_SetRivalBirchPosForDexUpgradeFemale {
    setobjectxy(LOCALID_RIVAL, 13, 10)
    setobjectxy(LOCALID_BIRCH, 14, 10)
    end
}

script LittlerootTown_EventScript_LeftLabAfterDexUpgrade {
    setvar(VAR_DEX_UPGRADE_JOHTO_STARTER_STATE, 3)
    return
}

script LittlerootTown_EventScript_HideMapNamePopup {
    setflag(FLAG_HIDE_MAP_NAME_POPUP)
    return
}

script LittlerootTown_EventScript_SetRivalLeftForRoute103 {
    setflag(FLAG_RIVAL_LEFT_FOR_ROUTE103)
    return
}

script LittlerootTown_EventScript_MoveRivalFromOldale {
    setvar(VAR_OLDALE_RIVAL_STATE, 2)
    setflag(FLAG_HIDE_OLDALE_TOWN_RIVAL)
    return
}

script LittlerootTown_EventScript_SetExitedHouseAfterLatiSSTicketEvent {
    setvar(VAR_LITTLEROOT_HOUSES_STATE_MAY, 5)
    setvar(VAR_LITTLEROOT_HOUSES_STATE_BRENDAN, 5)
    return
}

script LittlerootTown_EventScript_MoveMomToMaysDoor {
    setobjectxyperm(LOCALID_MOM, 14, 8)
    return
}

script LittlerootTown_EventScript_SetTwinPos {
    if (var(VAR_LITTLEROOT_TOWN_STATE) == 0) {
        goto(LittlerootTown_SetTwinGuardingRoutePos)
    }

    setobjectxyperm(LOCALID_TWIN, 10, 1)
    setobjectmovementtype(LOCALID_TWIN, MOVEMENT_TYPE_FACE_UP)
    return
}

script LittlerootTown_SetTwinGuardingRoutePos {
    setobjectxyperm(LOCALID_TWIN, 7, 2)
    setobjectmovementtype(LOCALID_TWIN, MOVEMENT_TYPE_FACE_DOWN)
    return
}

script LittlerootTown_EventScript_SetMomStandingInFrontOfDoorPos {
    clearflag(FLAG_HIDE_LITTLEROOT_TOWN_MOM_OUTSIDE)
    setobjectmovementtype(LOCALID_MOM, MOVEMENT_TYPE_FACE_DOWN)

    checkplayergender
    if (var(VAR_RESULT) == MALE) {
        call(LittlerootTown_SetMomInFrontOfDoorMale)
    }
    if (var(VAR_RESULT) == FEMALE) {
        call(LittlerootTown_SetMomInFrontOfDoorFemale)
    }
    return
}

script LittlerootTown_SetMomInFrontOfDoorMale {
    setobjectxyperm(LOCALID_MOM, 5, 9)
    return
}

script LittlerootTown_SetMomInFrontOfDoorFemale {
    setobjectxyperm(LOCALID_MOM, 14, 9)
    return
}

script LittlerootTown_EventScript_FatMan {
    msgbox (LittlerootTown_Text_CanUsePCToStoreItems, MSGBOX_NPC)
    if (flag(B_FLAG_FOLLOWERS_DISABLED)){
	clearflag (B_FLAG_FOLLOWERS_DISABLED)
	}else{
	setflag(B_FLAG_FOLLOWERS_DISABLED)}
	end
}

text LittlerootTown_Text_CanUsePCToStoreItems {
    "If you use a PC, you can store items\n"
    "and POKéMON.123.\p"
    "The power of science is staggering!"
}

script LittlerootTown_EventScript_Boy {
	msgbox (LittlerootTown_Text_BirchSpendsDaysInLab, MSGBOX_NPC)
	giveitem (ITEM_MEGA_RING)
	giveitem(ITEM_SWAMPERTITE, 100)
	end
}

text LittlerootTown_Text_BirchSpendsDaysInLab {
	"PROF. BIRCH spends days in his LAB\n"
	"studying, then he'll suddenly go out in\l"
	"the wild to do more research…\p"
	"When does PROF. BIRCH spend time\n"
	"at home?$"
}

script LittlerootTown_EventScript_Twin{
	lock
	faceplayer
	goto_if_set (FLAG_ADVENTURE_STARTED, LittlerootTown_EventScript_GoodLuck)
	goto_if_set (FLAG_RESCUED_BIRCH, LittlerootTown_EventScript_YouSavedBirch)
	goto_if_ne (VAR_LITTLEROOT_TOWN_STATE, 0, LittlerootTown_EventScript_GoSaveBirch)
	msgbox (LittlerootTown_Text_IfYouGoInGrassPokemonWillJumpOut, MSGBOX_DEFAULT)
	release
	end
}

script LittlerootTown_EventScript_GoodLuck {
    msgbox (LittlerootTown_Text_GoodLuckCatchingPokemon, MSGBOX_DEFAULT)
    release
    end
}

script LittlerootTown_EventScript_YouSavedBirch {
    special (GetPlayerBigGuyGirlString)
    msgbox (LittlerootTown_Text_YouSavedBirch, MSGBOX_DEFAULT)
    release
    end
}

script LittlerootTown_EventScript_GoSaveBirch {
    special (GetPlayerBigGuyGirlString)
    msgbox (LittlerootTown_Text_CanYouGoSeeWhatsHappening, MSGBOX_DEFAULT)
    closemessage
    applymovement (LOCALID_TWIN, Common_Movement_FaceOriginalDirection)
    waitmovement (0)
    setvar (VAR_LITTLEROOT_TOWN_STATE, 2)
    release
    end
}

script LittlerootTown_EventScript_GoSaveBirchTrigger{
	lockall
	applymovement (LOCALID_TWIN, Common_Movement_WalkInPlaceFasterRight)
	waitmovement(0)
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterLeft)
	waitmovement (0)
	special (GetPlayerBigGuyGirlString)
	msgbox (LittlerootTown_Text_CanYouGoSeeWhatsHappening, MSGBOX_DEFAULT)
	closemessage
	applymovement (LOCALID_TWIN, Common_Movement_FaceOriginalDirection)
	waitmovement (0)
	setvar (VAR_LITTLEROOT_TOWN_STATE, 2)
	releaseall
	end
}



script LittlerootTown_EventScript_NeedPokemonTriggerLeft {
    setvar(VAR_RESULT, NeedPokemonTriggerLeft)
    call(Handle_NeedPokemonTrigger)
}

script LittlerootTown_EventScript_NeedPokemonTriggerRight {
    setvar(VAR_RESULT, NeedPokemonTriggerRight)
    call(Handle_NeedPokemonTrigger)
}


script Handle_NeedPokemonTrigger {
    lockall

    if (var(VAR_RESULT) == NeedPokemonTriggerLeft) {
        applymovement(LOCALID_TWIN, TwinApproach_Left)
    } else {
        applymovement(LOCALID_TWIN, TwinApproach_Right)
    }
    waitmovement(0)

    call(LittlerootTown_EventScript_DangerousWithoutPokemon)

    if (var(VAR_RESULT) == NeedPokemonTriggerLeft) {
        applymovement(LOCALID_TWIN, TwinReturn_Left)
    } else {
        applymovement(LOCALID_TWIN, TwinReturn_Right)
    }
    waitmovement(0)

    releaseall
    end
}


script LittlerootTown_EventScript_DangerousWithoutPokemon {
    msgbox(LittlerootTown_Text_IfYouGoInGrassPokemonWillJumpOut, MSGBOX_DEFAULT)
    closemessage

    applymovement(LOCALID_TWIN, TwinPushPlayer)
    applymovement(OBJ_EVENT_ID_PLAYER, PlayerPushBack)
    waitmovement(0)

    msgbox(LittlerootTown_Text_DangerousIfYouDontHavePokemon, MSGBOX_DEFAULT)
    closemessage
    return
}

movement TwinApproach_Left {
    face_right
    delay_8
    disable_jump_landing_ground_effect
    jump_in_place_right
    delay_8
    walk_fast_right * 4
    walk_fast_up * 2
    walk_fast_left
    face_down
    step_end
}

movement TwinApproach_Right {
    face_right
    delay_8
    disable_jump_landing_ground_effect
    jump_in_place_right
    delay_8
    walk_fast_right * 3
    walk_fast_up * 2
    walk_fast_right
    face_down
    step_end
}

movement TwinReturn_Left {
    walk_right
    walk_down * 2
    walk_left * 4
    walk_up
    walk_in_place_faster_down
    step_end
}

movement TwinReturn_Right {
    walk_left
    walk_down
    walk_left * 3
    walk_in_place_faster_down
    step_end
}

movement TwinPushPlayer {
    walk_down
    step_end
}

movement PlayerPushBack {
    lock_facing_direction
    walk_down
    unlock_facing_direction
    step_end
}

text LittlerootTown_Text_DangerousIfYouDontHavePokemon{
	"It's dangerous if you don't have\n"
	"your own POKéMON.$"
}

text LittlerootTown_Text_IfYouGoInGrassPokemonWillJumpOut{
	"Um, um, um!\p"
	"If you go outside and go in the grass,\n"
	"wild POKéMON will jump out!$"
	}

text LittlerootTown_Text_GoodLuckCatchingPokemon{
	"Are you going to catch POKéMON?\n"
	"Good luck!$"
}

text LittlerootTown_Text_YouSavedBirch{
	"You saved PROF. BIRCH!\n"
	"I'm so glad!$"
}

text LittlerootTown_Text_CanYouGoSeeWhatsHappening{
	"Um, hi!\p"
	"There are scary POKéMON outside!\n"
	"I can hear their cries!\p"
	"I want to go see what's going on,\n"
	"but I don't have any POKéMON…\p"
	"Can you go see what's happening\n"
	"for me?$"
}

// Town Signs

script LittlerootTown_EventScript_TownSign{
	msgbox (LittlerootTown_Text_TownSign, MSGBOX_SIGN)
	end
}

text LittlerootTown_Text_TownSign{
	"LITTLEROOT TOWN\n"
	"“A town that can't be shaded any hue.”$"
}

script LittlerootTown_EventScript_BirchsLabSign{
	msgbox (LittlerootTown_Text_ProfBirchsLab, MSGBOX_SIGN)
	end
}

text LittlerootTown_Text_ProfBirchsLab{
	"PROF. BIRCH'S POKéMON LAB$"
}

script LittlerootTown_EventScript_BrendansHouseSign{
	lockall
	checkplayergender
	call_if_eq (VAR_RESULT, MALE, LittlerootTown_EventScript_PlayersHouseSignMale)
	call_if_eq (VAR_RESULT, FEMALE, LittlerootTown_EventScript_BirchsHouseSignFemale)
	releaseall
	end
}

script LittlerootTown_EventScript_PlayersHouseSignMale{
	msgbox (LittlerootTown_Text_PlayersHouse, MSGBOX_DEFAULT)
	return
}

script LittlerootTown_EventScript_BirchsHouseSignFemale{
	msgbox (LittlerootTown_Text_ProfBirchsHouse, MSGBOX_DEFAULT)
	return
}

script LittlerootTown_EventScript_MaysHouseSign{
	lockall
	checkplayergender
	call_if_eq (VAR_RESULT, MALE, LittlerootTown_EventScript_BirchsHouseSignMale)
	call_if_eq (VAR_RESULT, FEMALE, LittlerootTown_EventScript_PlayersHouseSignFemale)
	releaseall
	end
}

script LittlerootTown_EventScript_BirchsHouseSignMale{
	msgbox (LittlerootTown_Text_ProfBirchsHouse, MSGBOX_DEFAULT)
	return
}

script LittlerootTown_EventScript_PlayersHouseSignFemale{
	msgbox (LittlerootTown_Text_PlayersHouse, MSGBOX_DEFAULT)
	return
}

text LittlerootTown_Text_PlayersHouse{
	"{PLAYER}'s HOUSE$"
}

text LittlerootTown_Text_ProfBirchsHouse{
	"PROF. BIRCH'S HOUSE$"
}

//Get Running Shoes from Mum
script LittlerootTown_EventScript_GiveRunningShoesTrigger0{
	lockall
	setvar (VAR_GIVE_RUNNING_SHOES_TRIGGER, 0)
	setobjectxy (LOCALID_MOM, 10, 9)
	goto (LittlerootTown_EventScript_GiveRunningShoesTrigger)
	end
}

script LittlerootTown_EventScript_GiveRunningShoesTrigger1{
	lockall
	setvar (VAR_GIVE_RUNNING_SHOES_TRIGGER, 1)
	setobjectxy (LOCALID_MOM, 11, 9)
	goto (LittlerootTown_EventScript_GiveRunningShoesTrigger)
	end
}

script LittlerootTown_EventScript_GiveRunningShoesTrigger2{
	lockall
	setvar (VAR_GIVE_RUNNING_SHOES_TRIGGER, 2)
	goto (LittlerootTown_EventScript_GiveRunningShoesTrigger)
	end
}

script LittlerootTown_EventScript_GiveRunningShoesTrigger3{
	lockall
	setvar (VAR_GIVE_RUNNING_SHOES_TRIGGER, 3)
	goto (LittlerootTown_EventScript_GiveRunningShoesTrigger)
	end
}

script LittlerootTown_EventScript_GiveRunningShoesTrigger4{
	lockall
	setvar (VAR_GIVE_RUNNING_SHOES_TRIGGER, 4)
	goto (LittlerootTown_EventScript_GiveRunningShoesTrigger)
	end
}

script LittlerootTown_EventScript_GiveRunningShoesTrigger5{
	lockall
	setvar (VAR_GIVE_RUNNING_SHOES_TRIGGER, 5)
	goto (LittlerootTown_EventScript_GiveRunningShoesTrigger)
	end
}

script LittlerootTown_EventScript_GiveRunningShoesTrigger{
    call (LittlerootTown_EventScript_MomNoticePlayer)
	call (LittlerootTown_EventScript_SetHomeDoorCoords)
	msgbox (LittlerootTown_Text_WaitPlayer, MSGBOX_DEFAULT)
	closemessage
	call (LittlerootTown_EventScript_MomApproachPlayer)
	call (LittlerootTown_EventScript_GiveRunningShoes)
	checkplayergender
    call (LittlerootTown_EventScript_MomReturnHome)
	goto (LittlerootTown_EventScript_SetReceivedRunningShoes)
	end
}

script LittlerootTown_EventScript_SetHomeDoorCoords{
	checkplayergender
    if (var(VAR_RESULT) == MALE) {
	setvar (VAR_DOOR_X, 5)
	setvar (VAR_DOOR_Y, 8)
	}else{
	setvar (VAR_DOOR_X, 14)
	setvar (VAR_DOOR_Y, 8)
	}
	return
}

script LittlerootTown_EventScript_MomNoticePlayer{
	checkplayergender
    if (var(VAR_RESULT) == MALE) {
	applymovement (LOCALID_MOM, Common_Movement_WalkInPlaceFasterRight)
	}else{
	applymovement (LOCALID_MOM, Common_Movement_WalkInPlaceFasterLeft)
	}
	
	waitmovement (0)
	return
}

script LittlerootTown_EventScript_MomApproachPlayer{
	if (var(VAR_GIVE_RUNNING_SHOES_TRIGGER) <=1) {
		applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterDown)
	    waitmovement (0)
	    applymovement (LOCALID_MOM, LittlerootTown_Movement_MomApproachPlayer)
    }else{
	checkplayergender
    if (var(VAR_RESULT) == MALE) {
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterLeft)
	waitmovement (0)
	switch (var(VAR_GIVE_RUNNING_SHOES_TRIGGER)) {
		case 2:
            applymovement (LOCALID_MOM, LittlerootTown_Movement_MomApproachPlayerMale2)
        case 3:
			applymovement (LOCALID_MOM, LittlerootTown_Movement_MomApproachPlayerMale3)
		case 4:
            applymovement (LOCALID_MOM, LittlerootTown_Movement_MomApproachPlayerMale4)
		case 5:
		    applymovement (LOCALID_MOM, LittlerootTown_Movement_MomApproachPlayerMale5)
    }
	}else{
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterRight)
	waitmovement (0)
	switch (var(VAR_GIVE_RUNNING_SHOES_TRIGGER)) {
		case 2:
            applymovement (LOCALID_MOM, LittlerootTown_Movement_MomApproachPlayerFemale2)
        case 3:
			applymovement (LOCALID_MOM, LittlerootTown_Movement_MomApproachPlayerFemale3)
		case 4:
            applymovement (LOCALID_MOM, LittlerootTown_Movement_MomApproachPlayerFemale4)
		case 5:
		    applymovement (LOCALID_MOM, LittlerootTown_Movement_MomApproachPlayerFemale5)
    }
	}
	}
	waitmovement (0)
	return
}

script LittlerootTown_EventScript_MomReturnHome{
	if (var(VAR_GIVE_RUNNING_SHOES_TRIGGER) <=1) {
		applymovement (LOCALID_MOM, LittlerootTown_Movement_MomReturnHome)
		waitmovement (0)
	    return
    }else{
	checkplayergender
    if (var(VAR_RESULT) == MALE) {
	switch (var(VAR_GIVE_RUNNING_SHOES_TRIGGER)) {
		case 2:
            applymovement (LOCALID_MOM, LittlerootTown_Movement_MomReturnHomeMale2)
        case 3:
			applymovement (LOCALID_MOM, LittlerootTown_Movement_MomReturnHomeMale3)
		case 4:
            applymovement (LOCALID_MOM, LittlerootTown_Movement_MomReturnHomeMale4)
		case 5:
		    applymovement (LOCALID_MOM, LittlerootTown_Movement_MomReturnHomeMale5)
    }
	}else{
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterRight)
	waitmovement (0)
	switch (var(VAR_GIVE_RUNNING_SHOES_TRIGGER)) {
		case 2:
            applymovement (LOCALID_MOM, LittlerootTown_Movement_MomReturnHomeFemale2)
        case 3:
			applymovement (LOCALID_MOM, LittlerootTown_Movement_MomReturnHomeFemale3)
		case 4:
            applymovement (LOCALID_MOM, LittlerootTown_Movement_MomReturnHomeFemale4)
		case 5:
		    applymovement (LOCALID_MOM, LittlerootTown_Movement_MomReturnHomeFemale5)
    }
	}
	waitmovement (0)
	opendoor (VAR_DOOR_X, VAR_DOOR_Y)
	waitdooranim
	applymovement (LOCALID_MOM, LittlerootTown_Movement_MomExitThroughDoor)
	waitmovement (0)
	hideobjectat (LOCALID_MOM, MAP_LITTLEROOT_TOWN)
	closedoor (VAR_DOOR_X, VAR_DOOR_Y)
	waitdooranim
	return
	}
}

movement LittlerootTown_Movement_MomApproachPlayer{
	walk_up * 6
	step_end
}

movement LittlerootTown_Movement_MomApproachPlayerMale2{
	walk_right * 4
	step_end
}

movement LittlerootTown_Movement_MomApproachPlayerMale3{
	walk_right * 5
	step_end
}

movement LittlerootTown_Movement_MomApproachPlayerMale4{
	walk_right * 2
	step_end
}

movement LittlerootTown_Movement_MomApproachPlayerMale5{
	walk_right * 3
	step_end
}

movement LittlerootTown_Movement_MomApproachPlayerFemale2{
	walk_left * 3
	step_end
}

movement LittlerootTown_Movement_MomApproachPlayerFemale3{
	walk_left * 2
	step_end
}

movement LittlerootTown_Movement_MomApproachPlayerFemale4{
	walk_left * 5
	step_end
}

movement LittlerootTown_Movement_MomApproachPlayerFemale5{
	walk_left * 4
	step_end
}

movement LittlerootTown_Movement_MomReturnHome{
	walk_down * 5
	step_end
}

movement LittlerootTown_Movement_MomReturnHomeMale2{
	walk_left * 4
	walk_in_place_faster_up
	step_end
}

movement LittlerootTown_Movement_MomReturnHomeMale3{
	walk_left * 5
	walk_in_place_faster_up
	step_end
}

movement LittlerootTown_Movement_MomReturnHomeMale4{
	walk_left * 2
	walk_in_place_faster_up
	step_end
}

movement LittlerootTown_Movement_MomReturnHomeMale5{
	walk_left * 3
	walk_in_place_faster_up
	step_end
}

movement LittlerootTown_Movement_MomReturnHomeFemale2{
	walk_right * 3
	walk_in_place_faster_up
	step_end
}

movement LittlerootTown_Movement_MomReturnHomeFemale3{
	walk_right * 2
	walk_in_place_faster_up
	step_end
}

movement LittlerootTown_Movement_MomReturnHomeFemale4{
	walk_right * 5
	walk_in_place_faster_up
	step_end
}

movement LittlerootTown_Movement_MomReturnHomeFemale5{
	walk_right * 4
	walk_in_place_faster_up
	step_end
}

movement LittlerootTown_Movement_MomExitThroughDoor{
	walk_up
	step_end
}

script LittlerootTown_EventScript_Mom{
	lock
	faceplayer
	checkplayergender
	call (LittlerootTown_EventScript_SetHomeDoorCoords)
	call (LittlerootTown_EventScript_GiveRunningShoes)
	applymovement (LOCALID_MOM, Common_Movement_WalkInPlaceFasterUp)
	waitmovement (0)
	opendoor (VAR_DOOR_X, VAR_DOOR_Y)
	waitdooranim
	applymovement (LOCALID_MOM, LittlerootTown_Movement_MomExitThroughDoor)
	waitmovement (0)
	hideobjectat (LOCALID_MOM, MAP_LITTLEROOT_TOWN)
	closedoor (VAR_DOOR_X, VAR_DOOR_Y)
	waitdooranim
	goto (LittlerootTown_EventScript_SetReceivedRunningShoes)
	end
}

script LittlerootTown_EventScript_SetReceivedRunningShoes{
	removeobject (LOCALID_MOM)
	setflag (FLAG_SYS_B_DASH)
	setvar (VAR_LITTLEROOT_TOWN_STATE, 4)
	release
	end
}

script LittlerootTown_EventScript_GiveRunningShoes{
	msgbox (LittlerootTown_Text_WearTheseRunningShoes, MSGBOX_DEFAULT)
	playfanfare (MUS_OBTAIN_ITEM)
	message (LittlerootTown_Text_SwitchShoesWithRunningShoes)
	waitfanfare
	setflag (FLAG_RECEIVED_RUNNING_SHOES)
	msgbox (LittlerootTown_Text_ExplainRunningShoes, MSGBOX_DEFAULT)
	msgbox (LittlerootTown_Text_ComeHomeIfAnythingHappens, MSGBOX_DEFAULT)
	closemessage
	delay (30)
	return
}

text LittlerootTown_Text_WaitPlayer {
    "MOM: Wait, {PLAYER}!"
}

text LittlerootTown_Text_WearTheseRunningShoes {
    "MOM: {PLAYER}! {PLAYER}! Did you\n"
    "introduce yourself to PROF. BIRCH?\p"
    "Oh! What an adorable POKéMON!\n"
    "You got it from PROF. BIRCH. How nice!\p"
    "You're your father's child, all right.\n"
    "You look good together with POKéMON!\p"
    "Here, honey! If you're going out on an\n"
    "adventure, wear these RUNNING SHOES.\p"
    "They'll put a zip in your step!"
}

text LittlerootTown_Text_SwitchShoesWithRunningShoes {
    "{PLAYER} switched shoes with the\n"
    "RUNNING SHOES."
}

text LittlerootTown_Text_ExplainRunningShoes {
    "MOM: {PLAYER}, those shoes came with\n"
    "instructions.\p"
    "“Press the B Button while wearing these\n"
    "RUNNING SHOES to run extra-fast!\p"
    "“Slip on these RUNNING SHOES and race\n"
    "in the great outdoors!”"
}

text LittlerootTown_Text_ComeHomeIfAnythingHappens {
    "… … … … … … … …\n"
    "… … … … … … … …\p"
    "To think that you have your very own\n"
    "POKéMON now…\l"
    "Your father will be overjoyed.\p"
    "…But please be careful.\n"
    "If anything happens, you can come home.\p"
    "Go on, go get them, honey!"
}

text LittlerootTown_Text_BirchSomethingToShowYouAtLab{
	"PROF. BIRCH: Well, well, {PLAYER}{KUN}!\n"
	"That was good work out there!\p"
	"I knew there was something special\n"
	"about you when I first saw you,\l"
	"but I never expected this.\p"
	"Oh, yes. Do you still have the POKéDEX\n"
	"I gave you?\p"
	"I have something to show you.\n"
	"Let's go to my LAB.$"
}
